#!/usr/bin/perl
use strict;
use warnings;

# Read in the files from the spec file

my $package;
my $specfile = (glob('*.spec'))[0]
	or die "Unable to find a spec file";

open(my $spec, "<", $specfile)
	or die "Failed to open $specfile: $!\n";

my %files;
my $base_package_name;

# Read in all the installed filenames from the spec file
LINE:
while (my $line = <$spec>) {
	chomp $line;

	# Capture which (if any) package these files are part of
	if ($line =~ m/^\%files(?:\s+(\S+))?/) {
		$package = $1 || '';
		next LINE;
	}

	if (! $base_package_name && $line =~ m/^\s*Name\s*:\s*(\S+)/) {
		$base_package_name = $1;
		next LINE;
	}

	# Skip ahead to the %files declarations
	next LINE unless defined $package;

	if ($line =~ m#^/#) {
		$files{$package}{$line}++;
	}
}

close($spec)
	or die "$specfile: $!";

# Got the list of files, now let's process it to identify lang files
my %langs;
foreach my $package (keys %files) {
	foreach my $file (keys %{$files{$package}}) {
		# We'll take the existence of LC_MESSAGES as evidence of the support for
		# a particular language variant. We're lumping all variants together.
		# /usr/lib64/libreoffice/program/resource/zh_TW/LC_MESSAGES/vcl.mo
		if ($file =~ m#^.*/([a-z]+)(?:[_\-\@][a-zA-Z]+)?/LC_MESSAGES#) {
			my $lang = $1;
			$langs{$lang}++;
		}
	}
}

my $lang_join = join('|', sort keys %langs);
my $lang_re = qr#^.*?[/_\-]($lang_join)(?:[_\-\@][a-zA-Z\-]+)?(?:[/\.].*)?$#;

# Now use the languages pattern to sort the files.
my %extras;
foreach my $package (sort keys %files) {
	# Don't touch license files
	next if $package eq 'license';

	FILE:
	foreach my $file (sort keys %{$files{$package}}) {
		# Don't touch filter files
		next if $file =~ m#^/usr/lib64/libreoffice/share/filter/#;

		if ($file =~ $lang_re) {
			my $lang = lc($1);

			$extras{$lang}{$file}++;
		}
	}
}

# We've binned the files, now generate the ${custom}_extras
LANG:
foreach my $lang (sort keys %extras) {

	# Don't put the default English files into a separate package
	next LANG if $lang eq 'en';

	my $filename = "lang-${lang}_extras";

	open(my $fh, '>', $filename)
		or die "Failed to create $filename: $!";

	print $fh "## This file was auto-generated by $0\n";
	print $fh "## Manual changes will be lost\n";

	# Keep it sorted to simplify diffs
	foreach my $file (sort keys %{$extras{$lang}}) {
		print $fh "$file\n"
			or die "$filename: $!";
	}

	close($fh)
		or die "$filename: $!";

	# Also make it depend on the base package
	$filename .= '_requires';
	open($fh, '>', $filename)
		or die "Failed to create $filename: $!";
	print $fh "libreoffice\n";
	close($fh)
		or die "$filename: $!";
}
